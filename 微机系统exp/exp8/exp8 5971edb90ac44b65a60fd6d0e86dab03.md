# exp8

## 一. 基础性实验

1. 实现以不同的前景色和背景色显示16个字符A

```c
assume cs:code,ds:data

data segment
db 8192 dup(0)
data ends

code segment

putchar_with_color:                ; void putchar(char ch,uint16_t fcolor,uint16_t bcolor)
    ;INT 10h / AH = 09h - write character and attribute at cursor position.
    ;input:
    ;AL = character to display.
    ;BH = page number.
    ;BL = attribute.
    ;CX = number of times to write character.
    push bp
    mov bp,sp
    ;;;
    push bx
    push cx
    ;
    
    
    xor bx,bx
    ;低四位是前景色
    mov ax,[bp + 6]
    mov bl,al
    ;
    mov ax,[bp + 8]
    shl ax,4
    
    or bl,al
    
    mov ax,[bp + 4h]
    xor ah,ah
    
    
    mov cx,1
    
    mov ah,9
    int 10h
    
    pop cx
    pop bx
    mov sp,bp
    pop bp
    ret
    
 ;设置光标位置  
set_cursor_pos:             ;void set_cursor_pos(uint16_t x,uint16_t y)
    push bp
    mov bp,sp
    sub sp,100h
    
    push bx
    ;
    ;INT 10h / AH = 2 - set cursor position.
    ;input:
    ;DH = row.
    ;DL = column.
    ;BH = page number (0..7).
    mov ax,[bp + 4]
    mov dl,al
    
    mov ax,[bp + 6]
    mov dh,al
    
    xor bx,bx    
    
    xor ax,ax
    mov ah,2
    
    int 10h
     
    pop bx
    
    mov sp,bp
    pop bp
    ret
    
main:
	push bp
	mov bp,sp
	sub sp,100h                                          ;......ds.s
    
    mov word ptr [bp - 100h + 0h],0
   
    ;for(uint16_t i = 0; i < 16;i++)
		;       set_cursor_pos(i,0);
		;       putchar_with_color('A')

_main_loop: 
    mov ax,word ptr [bp - 100h + 0h]
    cmp ax,10h
    jae _main_loop_end
    ;set_cursor_pos
    
    mov ax,0
    push ax
    
    mov ax,word ptr [bp - 100h + 0h]
    push ax
    
    call set_cursor_pos
    add sp,4h
    
    ;putchar
    mov ax,0Fh
    push ax
    
    mov ax,word ptr [bp - 100h + 0h]
    push ax
    
    mov ax,'A'
    push ax
    
    call putchar_with_color
    
    add sp,6h
    
    mov ax,word ptr [bp - 100h + 0h]
    inc ax
    mov word ptr [bp - 100h + 0h],ax
    jmp _main_loop 
   
 
_main_loop_end:
    
	mov sp,bp
	pop bp
	ret
	
start:
	mov ax,data
	mov ds,ax
	
	mov ax,data
	mov ss,ax
	
	mov sp,2000h
	
	xor bp,bp

	call main

	mov ax,4c00h
	int 21h

code ends

end start
```

运行效果:

![Untitled](exp8%205971edb90ac44b65a60fd6d0e86dab03/Untitled.png)

1. 思考题: 若要将程序中的字符以斜线方式显示，应该如何修改指令？

改变y 坐标就行,x = y = i

```c
assume cs:code,ds:data

data segment
db 8192 dup(0)
data ends

code segment

putchar_with_color:                ; void putchar(char ch,uint16_t fcolor,uint16_t bcolor)
    ;INT 10h / AH = 09h - write character and attribute at cursor position.
    ;input:
    ;AL = character to display.
    ;BH = page number.
    ;BL = attribute.
    ;CX = number of times to write character.
    push bp
    mov bp,sp
    ;;;
    push bx
    push cx

    xor bx,bx
    ;低四位是前景色
    mov ax,[bp + 6]
    mov bl,al
    ;
    mov ax,[bp + 8]
    shl ax,4
    
    or bl,al
    
    mov ax,[bp + 4h]
    xor ah,ah
    
    
    mov cx,1
    
    mov ah,9
    int 10h
    
    pop cx
    pop bx
    mov sp,bp
    pop bp
    ret
    
    
    
set_cursor_pos:             ;void set_cursor_pos(uint16_t x,uint16_t y)
    push bp
    mov bp,sp
    sub sp,100h
    
    push bx
    ;
    ;INT 10h / AH = 2 - set cursor position.
    ;input:
    ;DH = row.
    ;DL = column.
    ;BH = page number (0..7).
    mov ax,[bp + 4]
    mov dl,al
    
    mov ax,[bp + 6]
    mov dh,al
    
    xor bx,bx    
    
    xor ax,ax
    mov ah,2
    
    int 10h
     
    pop bx
    
    mov sp,bp
    pop bp
    ret
    
main:
	push bp
	mov bp,sp
	sub sp,100h                                          ;......ds.s
    
  mov word ptr [bp - 100h + 0h],0
 
 
_main_loop: 
  mov ax,word ptr [bp - 100h + 0h]
  cmp ax,10h
  jae _main_loop_end
  ;set_cursor_pos
  
 
  mov ax,word ptr [bp - 100h + 0h]
  push ax
  
  push ax
  
  call set_cursor_pos
  add sp,4h
  
  ;putchar
  mov ax,0Fh
  push ax
  
  mov ax,word ptr [bp - 100h + 0h]
  push ax
  
  mov ax,'A'
  push ax
  
  call putchar_with_color
  
  add sp,6h
  
  mov ax,word ptr [bp - 100h + 0h]
  inc ax
  mov word ptr [bp - 100h + 0h],ax
  jmp _main_loop 
 
   
_main_loop_end:
    
	mov sp,bp
	pop bp
	ret
	
start:
	mov ax,data
	mov ds,ax
	
	mov ax,data
	mov ss,ax
	
	mov sp,2000h
	
	xor bp,bp

	call main

	mov ax,4c00h
	int 21h

code ends

end start
```

运行效果:

![Untitled](exp8%205971edb90ac44b65a60fd6d0e86dab03/Untitled%201.png)

## 二. 加强性实验

1. 实现运动的小车

```nasm
data segment
    db   4096  dup(0)
ends

code segment  

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

putchar_with_color:                ; void putchar(char ch,uint16_t fcolor,uint16_t bcolor,int count)
    ;INT 10h / AH = 09h - write character and attribute at cursor position.
    ;input:
    ;AL = character to display.
    ;BH = page number.
    ;BL = attribute.
    ;CX = number of times to write character.
    push bp
    mov bp,sp
    ;;;
    push bx
    push cx
    ;
    
    
    xor bx,bx
    ;低四位是前景色
    mov ax,[bp + 6]
    mov bl,al
    ;
    mov ax,[bp + 8]
    shl ax,4
    
    or bl,al
    
    mov ax,[bp + 4h]
    xor ah,ah
    
    
    mov cx,[bp + 0Ah]
    
    mov ah,9
    int 10h
    
    pop cx
    pop bx
    mov sp,bp
    pop bp
    ret
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    
    
if_keydown: 
    mov ah,1
    int 16h
    jz _if_keydown_no_key  
    
    xor ax,ax
    INT 16h ;/ AH = 00h - get keystroke from keyboard (no echo).
    mov ah,0  
    ret
    
_if_keydown_no_key: 
    xor ax,ax
    ret  
     

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    
set_cursor_pos:             ;void set_cursor_pos(uint16_t x,uint16_t y)
    push bp
    mov bp,sp
    sub sp,100h
    
    push bx
    ;
    ;INT 10h / AH = 2 - set cursor position.
    ;input:
    ;DH = row.
    ;DL = column.
    ;BH = page number (0..7).
    mov ax,[bp + 4]
    mov dl,al
    
    mov ax,[bp + 6]
    mov dh,al
    
    xor bx,bx    
    
    xor ax,ax
    mov ah,2
    
    int 10h
     
    pop bx
    
    mov sp,bp
    pop bp
    ret
                         
                                            
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;一次调用计次加一
interruption_proc:
    pusha         
    mov bp,sp
    push ds
           
    mov ax,data
    mov ds,ax
    
    mov ax,[1024 + 512]  
    inc ax
    mov [1024 + 512],ax
    
    pop ds
    mov sp,bp
    popa
    iret

  
;;;;;;;;;;;;;;;;;;;;;;
; 用中断调用次数来控制延迟时间
;;;;;;;;;;;;;;;;;;;;;
sleep: 
_sleep_loop:  
    mov ax,[1024 + 512]
    cmp ax,2
    jb _sleep_loop
    
    xor ax,ax
    mov [1024 + 512],ax
    ret
 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;  清屏....(实际上是输出黑色的 ' ')
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
cls:
    push bp
    mov bp,sp
    
    sub sp,4h               ;
    ;y = [bp - 4h + 0h] 
    ;x = [bp - 4h + 0h]     ;
    
    
    mov word ptr [bp - 4h + 0h],0
    

_cls_loop_y:
    mov ax, [bp - 4h + 0h]
    cmp ax,25
    jae _cls_loop_y_end
    
    mov word ptr [bp - 4h + 2h],0
   
_cls_loop_x:
    mov ax,word ptr [bp - 4h + 2h]
    cmp ax,80
    jae _cls_loop_x_end
    
    ;;;;;
    mov ax,[bp - 4h + 0h]
    push ax
    
    mov ax,[bp - 4h + 2h]
    push ax
    
    call set_cursor_pos
    add sp,4h
    
    ;;;;;;;     
    
    mov ax ,1
    push ax
    
    xor ax,ax
    push ax
    push ax
    
    mov ax,' '
    push ax
    
    call putchar_with_color
    
    add sp,8h
    
    ;
    mov ax,[bp - 4h + 2h]
    inc ax
    mov [bp -4h + 2h],ax 
    jmp _cls_loop_x
    
_cls_loop_x_end:
    mov ax,[bp - 4h + 0h]
    inc ax
    mov [bp - 4h + 0h],ax
    jmp _cls_loop_y  
    
_cls_loop_y_end:
    
    mov sp,bp
    pop bp
    ret

set_display_mode:
    push cx
    push dx
    
    mov ah,0
    mov al,3 ;设置屏幕显示模式为 80*25 彩色字符方式
    int 10H
    
    pop dx
    pop cx
    ret
         
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;  绘制小车
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  

draw_car:                       ;draw(uint16_t x ,uint16_t y)
    push bp
    mov bp,sp
    ;;;;
    mov ax,[bp + 6]
    push ax     
    
    mov ax,[bp + 4]
    push ax
    
    call set_cursor_pos
    add sp,4h
    ;;;;;;;;
    
    mov ax,5
    push ax
    
    xor ax,ax
    push ax
    
    mov ax,0Ch
    push ax
    
    mov ax,0DBH
    push ax
    
    call putchar_with_color   
    add sp,8h
    
    ;;;;draw second line
    
    mov ax,[bp + 6]
    add ax,1
    push ax
    
    mov ax,[bp + 4]
    sub ax,2
    push ax
    call set_cursor_pos
    add sp,4h
    ;;;;;
    mov ax,9
    push ax
    
    xor ax,ax
    push ax
    
    mov ax,4
    push ax
    
    mov ax,0DBH
    push ax
    call putchar_with_color
    add sp,8h
    ;;
    mov ax,[bp + 6]
    add ax,2
    push ax
    
    mov ax,[bp + 4]
    sub ax,1
    push ax
    
    call set_cursor_pos
    
    add sp,4h
    
    ;
    mov ax,1
    push ax
    
    mov ax,08H
    push ax
    
    mov ax,0Eh
    push ax 
    
    mov ax,09H
    push ax
    
    call putchar_with_color
    add sp,8h
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov ax,[bp + 6]
    add ax,2
    push ax
    
    mov ax,[bp + 4]
    add ax,5
    push ax
    
    call set_cursor_pos
    
    add sp,4h
    
    mov ax,1
    push ax
    
    mov ax,08H
    push ax
    
    mov ax,0Eh
    push ax 
    
    mov ax,09H
    push ax
    
    call putchar_with_color
    add sp,8h
    
    mov sp,bp
    pop bp
    ret
   
     
main:
    push bp
    mov bp,sp
    
	sub sp,100h   
	
	call set_display_mode
	
	;sleep 的实现需要这个
    ;install interruption
    mov word ptr [bp - 100h + 80h],0
    mov word ptr [bp - 100h + 82h],0

    xor ax,ax,
    mov es,ax   
    
    ;save old interruption procedure
    mov ax,es:[28 * 2]
    mov word ptr [bp - 100h + 80h],ax
    
    mov ax,es:[28 * 2 + 2]
    mov word ptr [bp - 100h + 82h],ax          
    
    ;;;;install new interruption procedure. 
    mov ax,cs
    mov ds,ax   
    
    lea dx, interruption_proc
    mov ax,251ch
    int 21h  
    
    mov ax,data
    mov ds,ax
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    
    ;x [bp - 100h + 0h]
    ;y [bp - 100h + 2h]
    
    mov [bp - 100h + 0h],0Ah
    mov [bp - 100h + 2h],0Ah
    
    
_loop_draw_:     
    call cls
    
    mov ax,[bp - 100h + 2h]
    push ax   
    
    mov ax,[bp - 100h + 0h]
    push ax
    
    call draw_car
    
    add sp,4h
    
    call sleep
    
    mov ax,[bp - 100h + 0h]
    inc ax
    cmp ax,  0A50H
    jb __check_key_down
    xor ax,ax
   
__check_key_down:
    mov [bp - 100h + 0h],ax
    call if_keydown
    test ax,ax
    jz _loop_draw_
    
    
    ;;;restore zero interruption.
    mov dx,word ptr [bp - 100h + 80h]
    
    mov ax,word ptr [bp - 100h + 82h]
    mov ds,ax
    mov ax,251ch
    int 21h
    
    mov ax,data
    mov ds,ax
    
    mov sp,bp
    pop bp
    ret
    
start:
; set segment registers:
    mov ax, data
    mov ds, ax
    mov es, ax
    mov ss, ax
    
    mov sp,1000h
    
    call main

    ; add your code here     
    mov ax, 4c00h ; exit to operating system.
    int 21h    
ends

end start ; set entry point and stop the assembler.
```

运行效果:

![Untitled](exp8%205971edb90ac44b65a60fd6d0e86dab03/Untitled%202.png)

![Untitled](exp8%205971edb90ac44b65a60fd6d0e86dab03/Untitled%203.png)