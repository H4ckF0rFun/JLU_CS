# exp2

## 一. 基础性实验:

1. 实现找3个数中的最大值

```c

assume cs:code,ds:data,ss:stack 

data segment
dd1 db 35h,78h,85h
dd2 db 00h
data ends

stack segment
db 4096 dup (0)
stack ends

code segment

start:
	mov ax,stack
	mov ss,ax

	mov ax,data
	mov ds,ax	

	mov sp,1000h
	xor bp,bp
	
	call main

	mov ax,4c00h
	int 21h

main:
	push bp
	mov bp,sp
	
	mov ax,3h
	push ax          ; data + 0 ,dd1
	
	mov ax,3h
	push ax
    	
	mov ax,0h
	push ax          ; data + 3,dd2
	
	call find_max
	add sp,6h

	mov sp,bp
	pop bp
	ret

find_max:		    ;/*find_max(uint8_t * in,WORD len ,uint8_t * out)*/
	push bp			;/* uint16_t max */
	mov bp,sp		;/* uint16_t i*/
	
	push ax			; save old register
	push bx
	sub sp,4h

	mov word ptr [bp - 2h],0		;/* i = 0*/

	jmp if_loop

loop_body:
	mov ax,[bp - 2h]
	test ax,ax
	jne cmp_var
	mov si,[bp + 4h]
	mov al,[si]
	mov [bp - 4h],al
	jmp for_xxx

cmp_var:
	mov si,[bp + 4h]
	mov bx,[bp - 2h]
	mov al,[si + bx]
	mov bl,[bp - 4h]
	cmp bl,al
	jae for_xxx			
	mov byte ptr [bp - 4h],al

for_xxx:
	mov ax,[bp - 2h]
	add ax,1h
	mov [bp - 2h],ax

if_loop:
	mov ax,[bp - 2h]
	cmp ax,[bp + 6h]
	jne loop_body
	; out result
	
	mov di,[bp + 8h]
	mov ax,[bp - 4h]
	
	mov [di],ax
	
	add sp,4h
	pop bx
	pop ax
	mov sp,bp
	pop bp
	ret

code ends

end start
```

运行结果:

![Untitled](exp2%2017a10966d36a44079f5c118dbbc37378/Untitled.png)

1. 思考题:
    1. END后面的标号指明的程序开始执行的地址
    2. 段的大小不确定，取决于自己用多少。从段地址开始，最多能访问到的空间有64kb.
    

## 二. 加强性实验

1. 代码功能: 将读取的字符串中的小写字母转换为大写字母

```c
assume cs:code,ds:data

data segment
db 8192 dup(0)
data ends

code segment

putchar:                    ; void putchar (char ascii)
    push bp
    mov bp,sp
       
    mov dx,[bp + 4]
    mov ah, 2
	int 21h          
	
	mov sp,bp
	pop bp
	ret
	
puts:                       ; void puts(char * string);
    push bp
    mov bp,sp
    sub sp,2h
    mov [bp - 2],0

__loop_putchar:
    mov si,[bp + 4]
    mov ax,[bp - 2]
    add si,ax
    mov al,[si]             ;load byte
    test al,al
    je __puts_end
    
    mov ah,0                ;putchar(ch)
    push ax
    call putchar
    add sp,2h
    
    mov ax,[bp - 2]
    add ax,1h
    mov [bp - 2],ax
    jmp __loop_putchar
    
__puts_end:
    mov ax,0xd
    push ax
    call putchar
    add sp,0x2 
    
    mov ax,0xa
    push ax
    call putchar
    add sp,0x2
    
    mov sp,bp
    pop bp
    
	ret
	
getchar:		;char getchar()
	mov ah,1h
	int 21h		;read character from standard 
	mov ah,0h	;input, with echo, result is stored in AL
	ret		
	
gets:			;int gets(char* buff)	return read bytes from stdin...
	push bp 
	mov bp,sp
	sub sp,2
	mov [bp - 2],0
__loop:
	call getchar
	cmp ax,0dh
	je __read_end
	mov di,[bp + 4]
	mov bx,[bp - 2]
	add di,bx	
	mov [di],al		;save read byte

	add bx,1h
	mov [bp - 2],bx
	
	jmp __loop

__read_end:
	mov ax,[bp - 2]
	mov di,[bp + 4]
	add di,ax
	mov byte ptr [di],0     ; '\0'
	
	; \r\n
	mov ax,0xd
    push ax
    call putchar
    add sp,0x2 
    
    mov ax,0xa
    push ax
    call putchar
    add sp,0x2
    
    
	mov sp,bp
	pop bp
	ret

convert:                ; char convert(char ch)  [61h,7ah]
    push bp
    mov bp,sp
    xor ax,ax
        
    mov al,[bp + 4]   
    
    cmp al,61h
    jb __convert_end
    
    cmp al,7ah
    ja __convert_end
    
    sub al,0x20

__convert_end:
    mov sp,bp
    pop bp
    ret

convert_string:      ; void convert_string(char * str)
    push bp 
    mov bp,sp
    sub sp,2h
    mov word ptr [bp - 2h],0
    
    mov si,[bp + 4h]
    jmp _if_str_end
    
_convert_str_loop: 
    mov ax,[bp - 2]
    mov si,[bp + 4h]
    add si,ax
    mov al,[si]
    mov ah,0
    
    push ax
    call convert
    add sp,2h
    
    mov bx,ax
    
    mov ax,[bp - 2]
    mov si,[bp + 4h]
    add si,ax
    mov [si],bl
    
    add ax,1
    mov [bp - 2],al
    
_if_str_end:
    mov ax,[bp - 2]
    mov si,[bp + 4h]
    add si,ax
    mov al,[si] 
    test al,al
    jne _convert_str_loop
    
    
    mov sp,bp
    pop bp
    ret
    
strcmp:                 ;int strcmp(char* s1,char *s2)      
    push bp
    mov bp,sp
    
    xor ax,ax
    mov si,[bp + 4]
    mov di,[bp + 6]
    
__cmp_loop:
    mov al,[si]
    sub al,[di]
    jne __cmp_end
    
    mov al,[si]
    test al,al
    je  __cmp_end
    inc si
    inc di
    jmp __cmp_loop
    
__cmp_end:
    mov sp,bp
    pop bp
    ret   

main:
	push bp
	mov bp,sp

	sub sp,100h                                          ;......ds.s
	; char buff[0x80] bp - 0x100    
	; char quit[] = "quit" bp - 0x80
	
_prompt:
	mov [bp - 0x100],'P'
	mov [bp - 0xff],'l'
	mov [bp - 0xfe],'e'
	mov [bp - 0xfd],'a'
	mov [bp - 0xfc],'s'
	mov [bp - 0xfb],'e'
	mov [bp - 0xfa],' '
	mov [bp - 0xf9],'i' 
	mov [bp - 0xf8],'n'
	mov [bp - 0xf7],'p'
	mov [bp - 0xf6],'u'
	mov [bp - 0xf5],'t'
	mov [bp - 0xf4],' '
	mov [bp - 0xf3],'s'
	mov [bp - 0xf2],'t'     
	mov [bp - 0xf1],'r'
	mov [bp - 0xf0],'i'
	mov [bp - 0xef],'n'
	mov [bp - 0xee],'g'
	mov [bp - 0xed],':'
	mov [bp - 0xec], 0
	
	mov [bp - 0x80],'q'
	mov [bp - 0x7f],'u'
	mov [bp - 0x7e],'i'
	mov [bp - 0x7d],'t'       
	mov [bp - 0x7c],0
	
	;puts(buff)
	mov ax,sp
	push ax
	call puts
	add sp,0x2
	
    ;gets(buff)
    mov ax,sp
    push ax
    call gets
    add sp,0x2
    

    ;strcmp(buff,"quit")
    mov ax,sp
    push ax
    add ax,80h
    push ax
    call strcmp
    add sp,0x4
    
    test ax,ax
    je __main_loop_end
    
    ;convert_string(buff)
    mov ax,sp
    push ax
    call convert_string
    add sp,2h
    
    ;puts(buff)
    mov ax,sp
    push ax
    call puts
    add sp,0x2
    jmp _prompt
    
__main_loop_end:
	mov sp,bp
	pop bp
	ret
	
start:
	mov ax,data
	mov ds,ax
	
	mov ax,data
	mov ss,ax
	
	mov sp,2000h
	
	xor bp,bp

	call main

	mov ax,4c00h
	int 21h

code ends

end start
```

运行结果:

![Untitled](exp2%2017a10966d36a44079f5c118dbbc37378/Untitled%201.png)

1. 思考题:
    1. 21h 和 21 的值不一样，对应的中断处理程序也不一样。
    2. 行