# exp6

## 一. 基础性实验

1. 找出最大值，并且以16进制方式显示

```nasm
int16toHexString(uint16_t value,char * str)
```

```nasm
uint16toHexStr:           ; uint16toHexStr(uint16 value,char * str)
    push bp
    mov bp,sp
    push cx
    push bx
    
    xor cx,cx
    xor bx,bx
    
_uint16toHexStr_loop:    
    cmp bx,4
    jz  _uint16toHexStr_ret

    mov ax,[bp + 4]  
    mov cx,bx
    shl cx,2
    
    shr ax,cl
    
    and ax,0fh
    cmp ax,9
    ja  _greater_than_9
    add ax,30h    
    jmp _uint16toHexStr_save
    
_greater_than_9:
    sub ax,0Ah
    add ax,41h
  
_uint16toHexStr_save:  
    mov di,[bp + 6]
    add di,bx
    mov [di],al     
    
    inc bx
    jmp _uint16toHexStr_loop
    
_uint16toHexStr_ret:    
    mov di,[bp + 6]
    add di,4
    mov byte ptr [di],0
     
    ;;;string reverse..
    mov di,[bp + 6]
    mov al,[di + 0]
    mov ah,[di + 3]
    
    mov [di + 3],al
    mov [di + 0],ah
    
    mov al,[di + 1]
    mov ah,[di + 2]
    
    mov [di + 1],ah
    mov [di + 2],al
      
    pop bx
    pop cx
    mov sp,bp
    pop bp
    ret
```

找最大值使用了exp5 中的代码:

```c
data segment
     db   4096  dup(0)
ends

code segment

;;;;;;;;;;;;;;;;;;;;;;;;;;;;

find_max_min:                  
    ;void find_max(uint16_t * arr, uint16_t len,uint16_t * _max,uint16_t * _min) 
    push bp 
    mov bp,sp
    
    sub sp,6h  
    ; uint16_t max [bp - 0x6 + 0x0]
    ; uint16_t min [bp - 0x6 + 0x2]
    ; uint16_t i   [bp - 0x6 + 0x4]
    
    push bx      
    push si
    
    mov word ptr [bp - 6h + 4h],0       ; i = 0
    
_find_max_min_loop:
    mov ax,[bp - 6h + 4h]
    mov bx,[bp + 6]
    cmp ax,bx
    jge _find_end
    
    cmp ax,0
    jne _find_normal
  
_find_first:
    mov si,[bp + 4h]
    mov ax,[bp - 6h + 4h]
    shl ax,1
    add si,ax
    
    mov ax,[si]
    mov [bp - 6h + 0h],ax
    mov [bp - 6h + 2h],ax
    jmp _find_loop_body_end
    
_find_normal:
    mov si,[bp + 4h]
    mov ax,[bp - 6h + 4h]
    shl ax,1
    add si,ax
    
    mov ax,[si]   
    
    mov bx,[bp - 6h + 0h]
    cmp ax,bx       
    
    jbe _find_compare_min
    mov [bp - 6h + 0h],ax
    
_find_compare_min:
    mov bx,[bp - 6h + 2h]
    cmp ax,bx
    jae _find_loop_body_end
    mov [bp - 6h + 2h],ax
    
_find_loop_body_end:
   
    mov ax,[bp - 6h + 4h]
    inc ax  
    mov [bp - 6h + 4h],ax
    
    jmp _find_max_min_loop
    
_find_end:    
    ;;save max and min .
    mov di,[bp + 8h]
    mov ax,[bp - 6h + 0h]
    mov [di],ax
    
    mov di,[bp + 0Ah]
    mov ax,[bp - 6h + 2h]
    mov [di],ax 
    
    pop si
    pop bx
    mov sp,bp
    pop bp
    ret
    
;;;;;;;;;;;;;;;;;;;;;;;;;;;;
putchar:                    ; void putchar (char ascii)
    push bp
    mov bp,sp
       
    mov dx,[bp + 4]
    mov ah, 2
	int 21h          
	
	mov sp,bp
	pop bp
	ret
	
puts:                       ; void puts(char * string);
    push bp
    mov bp,sp
    sub sp,2h
    mov word ptr [bp - 2],0

__loop_putchar:
    mov si,[bp + 4]
    mov ax,[bp - 2]
    add si,ax
    mov al,[si]             ;load byte
    test al,al
    je __puts_end
    
    mov ah,0                ;putchar(ch)
    push ax
    call putchar
    add sp,2h
    
    mov ax,[bp - 2]
    add ax,1h
    mov [bp - 2],ax
    jmp __loop_putchar
    
__puts_end:
    mov ax,0xd
    push ax
    call putchar
    add sp,0x2 
    
    mov ax,0xa
    push ax
    call putchar
    add sp,0x2
    
    mov sp,bp
    pop bp
    
	ret
 ;;;;;;;;;;;;;;;;;;;;;;;;;;;
uint16toHexStr:           ; uint16toHexStr(uint16 value,char * str)
    push bp
    mov bp,sp
    push cx
    push bx
    
    xor cx,cx
    xor bx,bx
    
_uint16toHexStr_loop:    
    cmp bx,4
    jz  _uint16toHexStr_ret

    mov ax,[bp + 4]  
    mov cx,bx
    shl cx,2
    
    shr ax,cl
    
    and ax,0fh
    cmp ax,9
    ja  _greater_than_9
    add ax,30h    
    jmp _uint16toHexStr_save
    
_greater_than_9:
    sub ax,0Ah
    add ax,41h
  
_uint16toHexStr_save:  
    mov di,[bp + 6]
    add di,bx
    mov [di],al     
    
    inc bx
    jmp _uint16toHexStr_loop
    
_uint16toHexStr_ret:    
    mov di,[bp + 6]
    add di,4
    mov byte ptr [di],0
     
    ;;;string reverse..
    mov di,[bp + 6]
    mov al,[di + 0]
    mov ah,[di + 3]
    
    mov [di + 3],al
    mov [di + 0],ah
    
    mov al,[di + 1]
    mov ah,[di + 2]
    
    mov [di + 1],ah
    mov [di + 2],al
      
    pop bx
    pop cx
    mov sp,bp
    pop bp
    ret
       

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;     
main:         
    push bp
    mov bp,sp
    sub sp,100h 
    
    ;uint16_t max [bp - 100h + 0h] 
    ;uint16_t min [bp - 100h + 2h] 
    
    ;uint16_t arr [bp - 100h + 4h]
    mov word ptr [bp - 100h + 4h],13     
    mov word ptr [bp - 100h + 6h],24
    mov word ptr [bp - 100h + 8h],92
    mov word ptr [bp - 100h + 0Ah],42
    mov word ptr [bp - 100h + 0Ch],25
    mov word ptr [bp - 100h + 0Eh],46
    mov word ptr [bp - 100h + 10h],75
    mov word ptr [bp - 100h + 12h],81
    mov word ptr [bp - 100h + 14h],53
    mov word ptr [bp - 100h + 16h],10
    
    ;find_max(uint16_t * arr, uint16_t len,uint16_t * _max,uint16_t * _min)          
    
    lea ax,[bp - 100h + 2h]
    push ax
    
    lea ax,[bp - 100h + 0h]
    push ax
    
    mov ax, 0Ah
    push ax
    
    lea ax,[bp - 100h + 4h]
    push ax
    
    call find_max_min
    
    add sp,08h
    ;
    ;char buf[0x20] - [bp - 100h + 20h]      
    lea ax,[bp - 100h + 20h]
    push ax
    
    mov ax,[bp - 100h + 0h]
    push ax
    
    call uint16toHexStr
    add sp, 4h
    
    lea ax,[bp - 100h + 20h]     
    push ax
    call puts
    add sp,2h
    
    
    mov sp,bp
    pop bp
    ret
    
start:
    mov ax,data
    mov ss,ax
    mov ds,ax
    
    mov sp,0
   
    call main
    
    mov ax,4c00h
    int 21h
ends

end start ; set entry point and stop the assembler.
```

运行效果:

![Untitled](exp6%2080004cec24054c4e8efc3b3ae6792d57/Untitled.png)

## 二. 加强性实验

1. 计算:   s = 1! + 2! + 3! + 4! + 5!

```c

data segment
    ; add your data here!
    pkey db "press any key...$"
ends

stack segment
    dw   128  dup(0)
ends
            
           
code segment
    
    
    
func :                      ; uint16_t func;
    ;uint16_t k = 1;
    ;uint16_t i,sum;
    ;for(int i = 1;i <= 5;i++){
    ;   k *= i;
    ;   sum += k;
    ;}
    ;return sum;        
    push bp 
    mov bp,sp
    
    sub sp,6h  
    push bx
    ;bp - 0x6 + 0x0 == k
    ;bp - 0x6 + 0x2 == sum
    ;bp - 0x6 + 0x4 == i
    
    mov word ptr [bp - 6h + 0h],1       ;k
    mov word ptr [bp - 6h + 2h],0       ;sum
    mov word ptr [bp - 6h + 4h],1       ;i
  
  
_func_loop:
    mov ax,[bp - 6h + 4h]  
    cmp ax,5h
    ja _func_ret
    
    ;k *= i
    mov ax,[bp - 6h + 0h]
    mov bx,[bp - 6h + 4h]
    
    mul bx
    mov [bp - 6h + 0h],ax
    
    ;sum += k;
    mov bx,[bp - 6h + 2h]
    add ax,bx
    mov [bp - 6h + 2h],ax
    
    ;i ++ 
    mov ax,[bp - 6h + 4h]
    inc ax
    mov [bp - 6h + 4h],ax
    jmp _func_loop
_func_ret:               
    ;return sum;
    mov ax,[bp - 6h + 2h]  
    
    pop bx
    mov sp,bp
    pop bp
    ret
    
start:
    mov ax,stack
    mov ss,ax
    mov ds,ax
    
    mov sp,100h
    
    call func
    
    
    mov ax,4c00h
    int 21h
ends

end start ; set entry point and stop the assembler.
```

运行结果:

计算的返回值在ax中: 

![Untitled](exp6%2080004cec24054c4e8efc3b3ae6792d57/Untitled%201.png)